---
title: Elixirでネストした構造体を全部Mapに変換する方法
tags:
  - Elixir
private: false
updated_at: '2023-08-14T06:34:10+09:00'
id: d48d24c6f14d14e5e30e
organization_url_name: fukuokaex
slide: false
ignorePublish: false
---
[Elixir Slack](https://elixir-slackin.herokuapp.com/)と[Elixir JP Slack](https://join.slack.com/t/elixirjp/shared_invite/zt-ae8m5bad-WW69GH1w4iuafm1tKNgd~w)を毎日眺めるのを日課にしています
そこでいろんなアイデアを得ることがあるのですが、今日は一つご紹介します

## やりたいこと

構造体とMapが深いネストで同居していている場合に、全てMapにしたい

## 架空の構造体

こういう構造体があったとします

```elixir
defmodule Hello, do: defstruct [:x]
```

## 最上階層のみを構造体からMapへ変換

```elixir
iex> s = %Hello{x: %Hello{x: 1}}

iex> Map.from_struct(s)
%{x: %Hello{x: 1}}
```

## ネスト構造の中のすべての構造体をMapへ変換

```elixir
defmodule Mnishiguchi.Map do
  def unstruct(%NaiveDateTime{} = encodable_struct), do: encodable_struct
  def unstruct(%DateTime{} = encodable_struct), do: encodable_struct
  def unstruct(%Time{} = encodable_struct), do: encodable_struct
  def unstruct(%Date{} = encodable_struct), do: encodable_struct

  def unstruct(struct_or_map) when is_map(struct_or_map) do
    struct_or_map
    |> Map.delete(:__struct__)
    |> Map.delete(:__meta__)
    |> Map.new(fn
      {k, v} when is_map(v) -> {k, unstruct(v)}
      {k, v} when is_list(v) -> {k, Enum.map(v, &unstruct/1)}
      kv -> kv
    end)
  end

  def unstruct(not_struct_or_map) do
    not_struct_or_map
  end
end

iex> s = %Hello{x: %Hello{x: %Hello{x: [%Hello{x: 1}, %Hello{x: %Hello{x: 1}}]}}}

iex> Mnishiguchi.Map.unstruct(s)
%{x: %{x: %{x: [%{x: 1}, %{x: %{x: 1}}]}}}
```

## Mapと構造体は若干異なる

ElixirのMapと構造体はほぼ同じなのですが、[挙動が若干異なります](https://elixir-lang.org/getting-started/structs.html#structs-are-bare-maps-underneath)

違いの一つに`Access behaviour`が挙げられます
構造体は`Access behaviour`を実装していないので、`[]`でアクセスしようとすると`UndefinedFunctionError`になります

```elixir
# Map
iex> m = %{x: 1}

iex> m.x
1

iex> m[:x]
1
```

```elixir
# 構造体
iex> s = %Hello{x: 1}

iex> s.x
1

iex> s[:x]
** (UndefinedFunctionError) function Hello.fetch/2 is undefined (Hello does not implement the Access behaviour)
    Hello.fetch(%Hello{x: 1}, :x)
    (elixir 1.12.2) lib/access.ex:285: Access.get/3
```
